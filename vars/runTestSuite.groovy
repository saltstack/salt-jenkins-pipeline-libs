def call(Map options) {

    def env = options.get('env')
    def String distro_name = options.get('distro_name')
    def String distro_version = options.get('distro_version')
    def String python_version = options.get('python_version')
    def String salt_target_branch = options.get('salt_target_branch')
    def String golden_images_branch = options.get('golden_images_branch')
    def String nox_env_name = options.get('nox_env_name')
    def String nox_passthrough_opts = options.get('nox_passthrough_opts')
    def Integer testrun_timeout = options.get('testrun_timeout')
    def Boolean run_full = options.get('run_full', true)
    def Boolean use_spot_instances = options.get('use_spot_instances', false)
    def String rbenv_version = options.get('rbenv_version', '2.6.3')
    def String jenkins_slave_label = options.get('jenkins_slave_label', 'kitchen-slave')
    def String notify_slack_channel = options.get('notify_slack_channel', '#jenkins-prod-pr')
    def String kitchen_driver_file = options.get('kitchen_driver_file', '/var/jenkins/workspace/driver.yml')
    def String kitchen_verifier_file = options.get('kitchen_verifier_file', '/var/jenkins/workspace/nox-verifier.yml')
    def String kitchen_platforms_file = options.get('kitchen_platforms_file', '/var/jenkins/workspace/nox-platforms.yml')
    def String[] extra_codecov_flags = options.get('extra_codecov_flags', [])
    def Boolean retrying = options.get('retrying', false)
    def Boolean upload_test_coverage = options.get('upload_test_coverage', true)
    def String vm_hostname = computeMachineHostname(
        env: env,
        distro_name: distro_name,
        distro_version: distro_version,
        python_version: python_version,
        nox_env_name: nox_env_name,
        extra_parts: extra_codecov_flags,
        retrying: retrying
    )

    def Boolean retry_build = false

    // In case we're testing golden images
    def Boolean golden_images_build = false
    def String ami_image_id = options.get('ami_image_id', '')
    def String vagrant_box = options.get('vagrant_box', '')
    def String vagrant_box_version = options.get('vagrant_box_version', '')
    def String vagrant_box_provider = options.get('vagrant_box_provider', 'parallels')
    def Boolean delete_vagrant_box = true

    if ( ami_image_id != '') {
        golden_images_build = true
    }

    def Boolean macos_build = false
    if ( distro_name == 'macosx' ) {
        macos_build = true
        if ( vagrant_box == '' ) {
            delete_vagrant_box = false
        } else {
            golden_images_build = true
        }
    }

    // Define a global pipeline timeout. This is the test run timeout with one(1) additional
    // hour to allow for artifacts to be downloaded, if possible.
    def global_timeout = testrun_timeout + 1

    echo """\
    Distro: ${distro_name}
    Distro Version: ${distro_version}
    Python Version: ${python_version}
    Salt Target Branch: ${salt_target_branch}
    Golden Images Branch: ${golden_images_branch}
    Nox Env Name: ${nox_env_name}
    Nox Passthrough Opts: ${nox_passthrough_opts}
    Test run timeout: ${testrun_timeout} Hours
    Global Timeout: ${global_timeout} Hours
    Full Testsuite Run: ${run_full}
    Use SPOT instances: ${use_spot_instances}
    RBEnv Version: ${rbenv_version}
    Jenkins Slave Label: ${jenkins_slave_label}
    Notify Slack Channel: ${notify_slack_channel}
    Kitchen Driver File: ${kitchen_driver_file}
    Kitchen Verifier File: ${kitchen_verifier_file}
    Kitchen Platforms File: ${kitchen_platforms_file}
    Computed Machine Hostname: ${vm_hostname}
    """.stripIndent()

    if ( ami_image_id != '' ) {
        echo """\
        Amazon AMI: ${ami_image_id}
        """.stripIndent()
    }
    if ( vagrant_box != '' ) {
        echo """\
        Vagrant Box: ${vagrant_box}
        Vagrant Box Version: ${vagrant_box_version}
        """.stripIndent()
    }

    def environ = [
        "SALT_KITCHEN_PLATFORMS=${kitchen_platforms_file}",
        "SALT_KITCHEN_VERIFIER=${kitchen_verifier_file}",
        "SALT_KITCHEN_DRIVER=${kitchen_driver_file}",
        "NOX_ENV_NAME=${nox_env_name.toLowerCase()}",
        'NOX_ENABLE_FROM_FILENAMES=true',
        "NOX_PASSTHROUGH_OPTS=${nox_passthrough_opts}",
        "SALT_TARGET_BRANCH=${salt_target_branch}",
        "GOLDEN_IMAGES_CI_BRANCH=${golden_images_branch}",
        "CODECOV_FLAGS=${distro_name}${distro_version},${python_version},${nox_env_name.toLowerCase().split('-').join(',')}",
        "RBENV_VERSION=${rbenv_version}",
        "TEST_SUITE=${python_version}",
        "TEST_PLATFORM=${distro_name}-${distro_version}",
        "FORCE_FULL=${run_full}",
        "TEST_VM_HOSTNAME=${vm_hostname}"
    ]

    if ( ami_image_id != '' ) {
        environ << "AMI_IMAGE_ID=${ami_image_id}"
    }

    if ( vagrant_box != '' ) {
        environ << "VAGRANT_BOX=${vagrant_box}"
        environ << "VAGRANT_BOX_VERSION=${vagrant_box_version}"
    }

    wrappedNode(jenkins_slave_label, global_timeout, notify_slack_channel) {
        withEnv(environ) {
            try {
                if ( macos_build ) {
                    // Cleanup old VMs
                    cleanupLocalVagrantBoxes()
                }

                // Checkout the repo
                stage('Clone') {
                    if ( golden_images_build == false ) {
                        checkout scm
                        sh 'git fetch --no-tags origin +refs/heads/${SALT_TARGET_BRANCH}:refs/remotes/origin/${SALT_TARGET_BRANCH}'
                    } else {
                        checkout([
                            $class: 'GitSCM',
                            branches: [
                                [name: "${salt_target_branch}"]
                            ],
                            doGenerateSubmoduleConfigurations: false,
                            extensions: [
                                [
                                    $class: 'CloneOption',
                                    noTags: false,
                                    reference: '',
                                    shallow: true
                                ]
                            ],
                            submoduleCfg: [],
                            userRemoteConfigs: [
                                [url: "https://github.com/saltstack/salt.git"]
                            ]
                        ])
                    }
                }

                // Setup the kitchen required bundle
                stage('Setup') {
                    if ( macos_build ) {
                        sh 'bundle install --with vagrant macos --without ec2 windows opennebula docker'
                        if ( golden_images_build ) {
                            // No coverage
                            writeFile encoding: 'utf-8', file: '.kitchen.local.yml', text: """\
                            verifier:
                              coverage: false
                            """.stripIndent()
                        }
                    } else {
                        sh 'bundle install --with ec2 windows --without docker macos opennebula vagrant'
                        if ( golden_images_build ) {
                            // Make sure we don't get any promoted images
                            writeFile encoding: 'utf-8', file: '.kitchen.local.yml', text: """\
                            driver:
                              image_search:
                                description: 'CI-STAGING *'
                            verifier:
                              coverage: false
                            """.stripIndent()
                        }
                    }
                }

                if ( macos_build == false ) {
                    if ( golden_images_build ) {
                        stage('Discover AMI') {
                            command_output = sh returnStdout: true, script:
                                '''
                                bundle exec kitchen diagnose $TEST_SUITE-$TEST_PLATFORM | grep 'image_id:' | awk '{ print $2 }'
                                '''
                            image_id = command_output.trim()
                            if ( image_id != '' ) {
                                addInfoBadge id: 'discovered-ami-badge', text: "Discovered AMI ${image_id} for this running instance"
                                createSummary(icon: "/images/48x48/attribute.png", text: "Discovered AMI: ${image_id}")
                            } else {
                                addWarningBadge id: 'discovered-ami-badge', text: "No AMI discovered to promote"
                                createSummary(icon: "/images/48x48/warning.png", text: "No AMI discovered to promote")
                            }
                            command_output = sh returnStdout: true, script:
                                '''
                                grep 'region:' $SALT_KITCHEN_DRIVER | awk '{ print $2 }'
                                '''
                            ec2_region = command_output.trim()
                            println "Discovered EC2 Region: ${ec2_region}"
                        }
                    }
                }

                stage('Create VM') {
                    if ( macos_build ) {
                        stage('Vagrant Box Details') {
                            sh '''
                            bundle exec kitchen diagnose $TEST_SUITE-$TEST_PLATFORM | grep 'box' ; echo "ExitCode: $?";
                            '''
                        }
                        sh '''
                        bundle exec kitchen create $TEST_SUITE-$TEST_PLATFORM; echo "ExitCode: $?";
                        '''
                        sh """
                        if [ -s ".kitchen/logs/${python_version}-${distro_name}-${distro_version}.log" ]; then
                            mv ".kitchen/logs/${python_version}-${distro_name}-${distro_version}.log" ".kitchen/logs/${python_version}-${distro_name}-${distro_version}-create.log"
                        fi
                        if [ -s ".kitchen/logs/kitchen.log" ]; then
                            mv ".kitchen/logs/kitchen.log" ".kitchen/logs/kitchen-create.log"
                        fi
                        """
                    } else {
                        retry(3) {
                            if ( use_spot_instances ) {
                                sh '''
                                cp -f ~/workspace/spot.yml .kitchen.local.yml
                                t=$(shuf -i 30-120 -n 1); echo "Sleeping $t seconds"; sleep $t
                                bundle exec kitchen create $TEST_SUITE-$TEST_PLATFORM || (bundle exec kitchen destroy $TEST_SUITE-$TEST_PLATFORM; rm .kitchen.local.yml; bundle exec kitchen create $TEST_SUITE-$TEST_PLATFORM); echo "ExitCode: $?";
                                '''
                            } else {
                                sh '''
                                t=$(shuf -i 30-120 -n 1); echo "Sleeping $t seconds"; sleep $t
                                bundle exec kitchen create $TEST_SUITE-$TEST_PLATFORM; echo "ExitCode: $?";
                                '''
                            }
                            sh """
                            if [ -s ".kitchen/logs/${python_version}-${distro_name}-${distro_version}.log" ]; then
                                mv ".kitchen/logs/${python_version}-${distro_name}-${distro_version}.log" ".kitchen/logs/${python_version}-${distro_name}-${distro_version}-create.log"
                            fi
                            if [ -s ".kitchen/logs/kitchen.log" ]; then
                                mv ".kitchen/logs/kitchen.log" ".kitchen/logs/kitchen-create.log"
                            fi
                            """
                        }
                        sh '''
                        bundle exec kitchen diagnose $TEST_SUITE-$TEST_PLATFORM | grep 'image_id:'
                        bundle exec kitchen diagnose $TEST_SUITE-$TEST_PLATFORM | grep 'instance_type:' -A5
                        rm -f .kitchen.local.yml
                        '''
                    }
                }

                try {
                    // Since we reserve for spot instances for a maximum of 6 hours,
                    // and we also set the maximum of some of the pipelines to 6 hours,
                    // the following timeout get's 15 minutes shaved off so that we
                    // have at least that ammount of time to download artifacts
                    timeout(time: testrun_timeout * 60 - 15, unit: 'MINUTES') {
                        stage('Converge VM') {
                            if ( macos_build ) {
                                sh '''
                                ssh-agent /bin/bash -c 'ssh-add ~/.vagrant.d/insecure_private_key; bundle exec kitchen converge $TEST_SUITE-$TEST_PLATFORM; echo "ExitCode: $?"'
                                '''
                            } else {
                                sh '''
                                ssh-agent /bin/bash -c 'ssh-add ~/.ssh/kitchen.pem; bundle exec kitchen converge $TEST_SUITE-$TEST_PLATFORM; echo "ExitCode: $?"'
                                '''
                            }
                            sh """
                            if [ -s ".kitchen/logs/${python_version}-${distro_name}-${distro_version}.log" ]; then
                                mv ".kitchen/logs/${python_version}-${distro_name}-${distro_version}.log" ".kitchen/logs/${python_version}-${distro_name}-${distro_version}-converge.log"
                            fi
                            if [ -s ".kitchen/logs/kitchen.log" ]; then
                                mv ".kitchen/logs/kitchen.log" ".kitchen/logs/kitchen-converge.log"
                            fi
                            """
                        }
                        stage("Run ${python_version.capitalize()} Tests") {
                            withEnv(["DONT_DOWNLOAD_ARTEFACTS=1"]) {
                                sh 'bundle exec kitchen verify $TEST_SUITE-$TEST_PLATFORM; echo "ExitCode: $?";'
                            }
                        }
                    }
                } finally {
                    try {
                        sh """
                        if [ -s ".kitchen/logs/${python_version}-${distro_name}-${distro_version}.log" ]; then
                            mv ".kitchen/logs/${python_version}-${distro_name}-${distro_version}.log" ".kitchen/logs/${python_version}-${distro_name}-${distro_version}-verify.log"
                        fi
                        if [ -s ".kitchen/logs/kitchen.log" ]; then
                            mv ".kitchen/logs/kitchen.log" ".kitchen/logs/kitchen-verify.log"
                        fi
                        """

                        if ( retrying == false ) {
                            // Let's see if we should retry the build
                            def List<String> conditions_found = []
                            retry_build = checkRetriableConditions(conditions_found, ".kitchen/logs/${python_version}-${distro_name}-${distro_version}-verify.log")
                        }

                        stage('Download Artefacts') {
                            withEnv(["ONLY_DOWNLOAD_ARTEFACTS=1"]){
                                sh '''
                                bundle exec kitchen verify $TEST_SUITE-$TEST_PLATFORM || exit 0
                                '''
                            }
                            sh """
                            if [ -s ".kitchen/logs/${python_version}-${distro_name}-${distro_version}.log" ]; then
                                mv ".kitchen/logs/${python_version}-${distro_name}-${distro_version}.log" ".kitchen/logs/${python_version}-${distro_name}-${distro_version}-download.log"
                            fi
                            if [ -s ".kitchen/logs/kitchen.log" ]; then
                                mv ".kitchen/logs/kitchen.log" ".kitchen/logs/kitchen-download.log"
                            fi
                            """
                        }
                        archiveArtifacts(
                            artifacts: "artifacts/*,artifacts/**/*,.kitchen/logs/*-create.log,.kitchen/logs/*-converge.log,.kitchen/logs/*-verify.log,.kitchen/logs/*-download.log,artifacts/xml-unittests-output/*.xml",
                            allowEmptyArchive: true
                        )
                        junit 'artifacts/xml-unittests-output/*.xml'
                    } finally {
                        stage('Cleanup') {
                            sh '''
                            bundle exec kitchen destroy $TEST_SUITE-$TEST_PLATFORM; echo "ExitCode: $?";
                            '''
                            try {
                                if ( delete_vagrant_box ) {
                                    sh """
                                    vagrant box remove --force --provider=${vagrant_box_provider} --box-version=${vagrant_box_version} ${vagrant_box} || true
                                    """
                                }
                            } catch (Exception delete_vagrant_box_error) {
                                println "Failed to delete vagrant box: ${delete_vagrant_box_error}"
                            }
                            try {
                                sh """
                                vagrant box prune --keep-active-boxes --force --provider=${vagrant_box_provider} --box-version=${vagrant_box_version} ${vagrant_box} || true
                                """
                            } catch (Exception prune_vagrant_box_error) {
                                println "Failed to prune vagrant box: ${prune_vagrant_box_error}"
                            }
                        }
                        cleanWs notFailBuild: true
                        if ( upload_test_coverage == true ) {
                            stage('Upload Coverage') {
                                if ( run_full ) {
                                    def distro_strings = [
                                        distro_name,
                                        distro_version
                                    ]
                                    def report_strings = (
                                        [python_version] + nox_env_name.split('-') + extra_codecov_flags
                                    ).flatten()
                                    uploadCodeCoverage(
                                        report_path: 'artifacts/coverage/tests.xml',
                                        report_name: "${distro_strings.join('-')}-${report_strings.join('-')}-tests",
                                        report_flags: ([distro_strings.join('')] + report_strings + ['tests']).flatten()
                                    )
                                    sleep(
                                        time: 5,
                                        unit: 'SECONDS'
                                    )
                                    uploadCodeCoverage(
                                        report_path: 'artifacts/coverage/salt.xml',
                                        report_name: "${distro_strings.join('-')}-${report_strings.join('-')}-salt",
                                        report_flags: ([distro_strings.join('')] + report_strings + ['salt']).flatten()
                                    )
                                }
                            }
                        }
                    }
                }
            } finally {
                if ( macos_build ) {
                    // Cleanup old VMs
                    cleanupLocalVagrantBoxes()
                }
            }
        }
    }

    if ( retrying == false && retry_build == true) {
        throw new Exception('retry-build')
    }

}
// vim: ft=groovy ts=4 sts=4 et
